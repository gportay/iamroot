# Maintainer: GaÃ«l PORTAY <gael.portay@collabora.com>

pkgname=iamroot-git
pkgver=r1011.a4f354c
pkgrel=1
pkgdesc='Emulating the syscall chroot(2) in an unpriviliged process'
arch=('x86_64')
url="https://github.com/gportay/$pkgname"
license=('GPL')
makedepends=('lib32-glibc'
             'i386-musl'
             'kernel-headers-musl-i386'
             'musl'
             'kernel-headers-musl'
             'armhf-musl'
             'kernel-headers-musl-armhf'
             'aarch64-linux-musl'
             'aarch64-linux-gnu-gcc'
             'kernel-headers-musl-aarch64'
             'git'
             'asciidoctor')
checkdepends=('shellcheck')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
options=('!strip')
source=("$pkgname::git+https://github.com/gportay/${pkgname%-git}.git")
sha256sums=('SKIP')
validpgpkeys=('8F3491E60E62695ED780AC672FA122CA0501CA71')

pkgver() {
	cd "$srcdir/$pkgname"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$pkgname"
	ln -sf support/makefile
	touch local.mk
	sed -e "/^VERSION = /s,\w$,$pkgver," -i Makefile
	sed -e "/^VERSION=/s,\w$,$pkgver," -i iamroot-shell
	sed -e "/^:man source:/s,\w$,$pkgver," -i iamroot-shell.1.adoc
	sed -e "/^:man source:/s,\w$,$pkgver," -i iamroot.7.adoc
}

build() {
	cd "$srcdir/$pkgname"
	make i686/libiamroot-linux.so.2
	make i686/libiamroot-musl-i386.so.1
	make x86_64/libiamroot-linux-x86-64.so.2
	make x86_64/libiamroot-musl-x86_64.so.1
	make CFLAGS= armhf/libiamroot-linux-armhf.so.3
	make CFLAGS= armhf/libiamroot-musl-armhf.so.1
	make CFLAGS= aarch64/libiamroot-linux-aarch64.so.1
	make CFLAGS= aarch64/libiamroot-musl-aarch64.so.1
	make doc
}

check() {
	cd "$srcdir/$pkgname"
	make -k check
}

package() {
	cd "$srcdir/$pkgname"
	make PREFIX=/usr DESTDIR="$pkgdir" install-exec-i686-linux.2
	make PREFIX=/usr DESTDIR="$pkgdir" install-exec-i686-musl-i386.1
	make PREFIX=/usr DESTDIR="$pkgdir" install-exec-x86_64-linux-x86-64.2
	make PREFIX=/usr DESTDIR="$pkgdir" install-exec-x86_64-musl-x86_64.1
	make PREFIX=/usr DESTDIR="$pkgdir" install-exec-aarch64-linux-aarch64.1
	make PREFIX=/usr DESTDIR="$pkgdir" install-exec-aarch64-musl-aarch64.1
	make PREFIX=/usr DESTDIR="$pkgdir" install-exec-armhf-linux-armhf.3
	make PREFIX=/usr DESTDIR="$pkgdir" install-exec-armhf-musl-armhf.1
	make PREFIX=/usr DESTDIR="$pkgdir" install-doc
	make PREFIX=/usr DESTDIR="$pkgdir" install-bash-completion
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
