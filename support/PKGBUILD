# Maintainer: GaÃ«l PORTAY <gael.portay@collabora.com>

pkgname=iamroot-git
pkgver=master
pkgrel=1
pkgdesc='Emulating the syscall chroot(2) in an unpriviliged process'
arch=('x86_64')
url="https://github.com/gportay/$pkgname"
license=('GPL')
makedepends=('aarch64-linux-gnu-gcc' 'aarch64-linux-musl' 'musl' 'kernel-headers-musl' 'kernel-headers-musl-aarch64' 'asciidoctor')
checkdepends=('shellcheck')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("$pkgname::git+https://github.com/gportay/${pkgname%-git}.git")
sha256sums=('SKIP')
validpgpkeys=('8F3491E60E62695ED780AC672FA122CA0501CA71')

pkgver() {
	cd "$srcdir/$pkgname"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$pkgname"
	ln -sf support/makefile
	touch local.mk
	sed -e "/^VERSION = /s,\w$,$pkgver," -i Makefile
	sed -e "/^VERSION=/s,\w$,$pkgver," -i iamroot-shell
	sed -e "/^:man source:/s,\w$,$pkgver," -i iamroot-shell.1.adoc
	sed -e "/^:man source:/s,\w$,$pkgver," -i iamroot.7.adoc
}

build() {
	cd "$srcdir/$pkgname"
	make x86_64/libiamroot-linux-x86-64.so.2
	make x86_64/libiamroot-musl-x86_64.so.1
	make CFLAGS= aarch64/libiamroot-linux-aarch64.so.1
	make CFLAGS= aarch64/libiamroot-musl-aarch64.so.1
	make doc
}

check() {
	cd "$srcdir/$pkgname"
	make -k check
}

package() {
	cd "$srcdir/$pkgname"
	make PREFIX=/usr DESTDIR="$pkgdir" install-exec-x86_64-linux-x86-64.2
	make PREFIX=/usr DESTDIR="$pkgdir" install-exec-x86_64-musl-x86_64.1
	make PREFIX=/usr DESTDIR="$pkgdir" install-exec-aarch64-linux-aarch64.1
	make PREFIX=/usr DESTDIR="$pkgdir" install-exec-aarch64-musl-aarch64.1
	make PREFIX=/usr DESTDIR="$pkgdir" install-doc
	make PREFIX=/usr DESTDIR="$pkgdir" install-bash-completion
	install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
