# Maintainer: GaÃ«l PORTAY <gael.portay@gmail.com>

pkgname=iamroot-armhf-git
pkgver=18
pkgrel=1
pkgdesc='Emulating the syscall chroot(2) in an unpriviliged process (armhf)'
arch=('x86_64')
url="https://github.com/gportay/${pkgname%-armhf-git}"
license=('GPL')
makedepends=('armv7-eabihf-glibc-bleeding-edge-toolchain'
             'armv7-eabihf-musl-bleeding-edge-toolchain'
             'git'
             'patchelf'
             'asciidoctor')
checkdepends=('shellcheck')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
options=('!strip')
source=("$pkgname::git+https://github.com/gportay/${pkgname%-armhf-git}.git")
sha256sums=('SKIP')
validpgpkeys=('8F3491E60E62695ED780AC672FA122CA0501CA71')

pkgver() {
	cd "$srcdir/$pkgname"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$pkgname"
	ln -sf support/linux.mk makefile
	touch local.mk
	sed -e "/^VERSION = /s,\w\+$,$pkgver," -i Makefile
	sed -e "/^VERSION=/s,\w\+$,$pkgver," -i ido
	sed -e "/^VERSION=/s,\w\+$,$pkgver," -i ish
	sed -e "/^:man source:/s,\w\+$,$pkgver," -i ido.1.adoc
	sed -e "/^:man source:/s,\w\+$,$pkgver," -i ish.1.adoc
	sed -e "/^:man source:/s,\w\+$,$pkgver," -i iamroot.7.adoc
}

build() {
	cd "$srcdir/$pkgname"
	make CFLAGS= PREFIX=/usr armhf/libiamroot-linux-armhf.so.3
	make CFLAGS= PREFIX=/usr armhf/libiamroot-musl-armhf.so.1
}

check() {
	cd "$srcdir/$pkgname"
	make -k check
}

package() {
	cd "$srcdir/$pkgname"
	make PREFIX=/usr DESTDIR="$pkgdir" install-exec-armhf-linux-armhf.3
	make PREFIX=/usr DESTDIR="$pkgdir" install-exec-armhf-musl-armhf.1
	install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
