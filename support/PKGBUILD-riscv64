# Maintainer: GaÃ«l PORTAY <gael.portay@gmail.com>

pkgname=iamroot-riscv64-git
pkgver=23
pkgrel=1
pkgdesc='Emulating the syscall chroot(2) in an unpriviliged process (riscv64)'
arch=('x86_64')
url="https://github.com/gportay/${pkgname%-riscv64-git}"
license=('GPL')
makedepends=('riscv64-lp64d-glibc-bleeding-edge-toolchain'
             'riscv64-lp64d-musl-bleeding-edge-toolchain'
             'git'
             'patchelf'
             'asciidoctor')
checkdepends=('shellcheck')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
options=('!strip')
source=("$pkgname::git+https://github.com/gportay/${pkgname%-riscv64-git}.git")
sha256sums=('SKIP')
validpgpkeys=('8F3491E60E62695ED780AC672FA122CA0501CA71')

pkgver() {
	cd "$srcdir/$pkgname"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$pkgname"
	ln -sf support/linux.mk makefile
	touch local.mk
	sed -e "/^VERSION = /s,\w\+$,$pkgver," -i Makefile
	sed -e "/^VERSION=/s,\w\+$,$pkgver," -i ido
	sed -e "/^VERSION=/s,\w\+$,$pkgver," -i ish
	sed -e "/^:man source:/s,\w\+$,$pkgver," -i ido.1.adoc
	sed -e "/^:man source:/s,\w\+$,$pkgver," -i ish.1.adoc
	sed -e "/^:man source:/s,\w\+$,$pkgver," -i iamroot.7.adoc
	sed -e "/^:man source:/s,\w\+$,$pkgver," -i ld-iamroot.so.8.adoc
}

build() {
	cd "$srcdir/$pkgname"
	make CFLAGS= PREFIX=/usr riscv64/libiamroot-linux-riscv64-lp64d.so.1
	make CFLAGS= PREFIX=/usr riscv64/libiamroot-musl-riscv64.so.1
}

check() {
	cd "$srcdir/$pkgname"
	make -k check
}

package() {
	cd "$srcdir/$pkgname"
	make PREFIX=/usr DESTDIR="$pkgdir" install-exec-riscv64-linux-riscv64-lp64d.1
	make PREFIX=/usr DESTDIR="$pkgdir" install-exec-riscv64-musl-riscv64.1
	make PREFIX=/usr DESTDIR="$pkgdir" install-support-riscv64
	install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
